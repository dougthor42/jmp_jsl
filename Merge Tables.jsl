// Set import preferences so that it uses Excel labels for column names
Preferences(Excel Has Labels(1), Excel Selection(0));

// Opens Dialog for user to select file.
file = Pick File ("Select a SWG file", "", {"SWG Files|xls;xlsx;xlsm","All Files|*"});

// Generate the path to the other file generated by the Reedholm system
file2 = Munger(file, 0, "SWG.xls", "SWP.xls");

// Print to log for debug purposes
Print (file);
Print (file2);

// open the files
Open (file);
Open (file2);

// Create a list of the data tables that were opened. Used later to close them
openTables = List();
For( i = 1, i <= N Table(), i++,
	Insert Into( openTables, Data Table( i ) );
);

// Join tables
dt1 = Data Table( "Graph_Data1" ) << Join(
	With( Data Table( "Graphs1" ) ),
	Merge Same Name Columns,
	By Matching Columns( :Graph_ID = :Graph_ID ),
	Drop multiples( 0, 0 ),
	Name( "Include non-matches" )(0, 0),
	Preserve main table order( 1 ),
	Output Table( "temp1" )
);

dt2 = Data Table( "temp1" ) << Join(
	With( Data Table( "GraphList_Facts1" ) ),
	Merge Same Name Columns,
	By Matching Columns( :Graph_ID = :Graph_ID ),
	Drop multiples( 0, 0 ),
	Name( "Include non-matches" )(0, 0),
	Preserve main table order( 1 ),
	Output Table( "temp2" )
);

Data Table( "temp2" ) << Join(
	With( Data Table( "GraphListNames1" ) ),
	Merge Same Name Columns,
	By Matching Columns( :GraphList_ID = :GraphList_ID ),
	Drop multiples( 0, 0 ),
	Name( "Include non-matches" )(0, 0),
	Preserve main table order( 1 ),
	Output Table( "final" )
);

// Close the temp tables
Close(dt1, NoSave);
Close(dt2, NoSave);

// Close the tables opened by the excel files.
For( i = 1, i <= N Items(openTables), i++,
	Close( openTables[i], NoSave);
);